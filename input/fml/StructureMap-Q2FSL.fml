/// url = 'https://interop.aphp.fr/ig/fhir/dm/StructureMap/Q2FSL'
/// name = 'Q2FSL'
/// title = 'Questionnaire to FHIR Semantic Layer'
/// status = 'draft'
/// description = 'Transforms QuestionnaireResponse based on Questionnaire Usage Variables socles into FHIR resources conforming to DM profiles'

uses "http://hl7.org/fhir/StructureDefinition/QuestionnaireResponse" alias QuestionnaireResponse as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" alias Bundle as target

// Main entry point: Transform QuestionnaireResponse to Bundle
group QuestionnaireResponseToBundle(source src : QuestionnaireResponse, target bundle : Bundle) {
  // Initialize Bundle
  src -> bundle.id = uuid() "bundle-id";
  src -> bundle.type = 'collection' "bundle-type";
  src -> bundle.timestamp = (now()) "setTimestamp";

  // Create Patient resource
  src -> bundle.entry as patientEntry then {
    src -> patientEntry.resource = create('Patient') as patient then {
      src then CreatePatient(src, patient) "create-patient";
      src -> patient.id as patientId, patientEntry.fullUrl = append('urn:uuid:', patientId) "set-patient-fullUrl";
  
      // Create Encounters from PMSI data
      src then CreateEncounters(src, patient, bundle) "create-encounters";

      // Create Laboratory Observations
      src then CreateLaboratoryObservations(src, patient, bundle) "create-lab-observations";

      // Create Medication Requests
      src then CreateMedicationRequests(src, patient, bundle) "create-medication-requests";

      // Create Vital Sign Observations
      src then CreateVitalSignObservations(src, patient, bundle) "create-vital-signs";

      // Create habitus Observations
      src then CreateHabitusObservation(src, patient, bundle) "createHabitus";

    } "create-patient-resource";
  } "patient-entry";
}

// Group: Create Patient from QuestionnaireResponse
group CreatePatient(source src : QuestionnaireResponse, target patient : Patient) {
  src -> patient.id = uuid() "patient-id";

  src -> patient.meta = create('Meta') as meta then {
    src -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMPatient' "patient-profile";
  } "patient-meta";

  // Extract patient identity data from linkId 4647259356106 > 2958000860428
  src.item as socioDemo where (linkId = '4647259356106') then {
    socioDemo.item as identity where (linkId = '2958000860428') then {

      // Patient name (linkId 8605698058770 = family, 6214879623503 = given) - Je ne sais pas pourquoi on a mis ces informations dans le QR. En postulant que ce serait par rapport à l'a présence d'un INS, il faut alors un officialName, d'ou la règle, éteinte, "set-use"
      identity where repeat(item).where(linkId='8605698058770').exists() or repeat(item).where(linkId='6214879623503').exists() -> patient.name = create('HumanName') as name then {
        identity.item as familyItem where (linkId = '8605698058770') then {
          familyItem.answer as ans -> name.family = (ans.valueString) "set-family";
        } "extract-family";
        identity.item as givenItem where (linkId = '6214879623503') then {
          givenItem.answer as ans -> name.given = (ans.valueString) "set-given";
        } "extract-given";
        //identity -> name.use = 'official' "set-use" ;
      } "set-name";

      // NIR identifier (linkId 5711960356160) - j'ai considéré la slice NSS qui est impropre, mais pas plus que quelque slice INS que ce soit... INS-NIR serait sans doute la plus adapté, mais alors il faudrait les traits identifiants (on ne les as pas) - et on n'a pas la slice INS-NIR dans le profil. En fait, il n'y a pas de solution prévu pour le NIR seul de l'ayant droit.
      identity.item as nirItem where (linkId = '5711960356160') then {
        nirItem.answer as ans -> patient.identifier = create('Identifier') as nss then {
          ans -> nss.system = 'urn:oid:1.2.250.1.213.1.4.8' "nss-system";
          ans -> nss.value = (ans.valueString) "nss-value";
          ans -> nss.type = cc('http://terminology.hl7.org/CodeSystem/v2-0203', 'NH') "nss-type";
        } "set-nss";
      } "extract-nir";

      // INS identifier (linkId 3764723550987) - pour l'heure ça correspond à la slice INS-C, mais ça n'a pas de sens. La slice INS-NIR ferait le taf, mais : (1) redondant avec le NIR, (2) pas les traits identifiant et (3) pas la slice dans le profil.
      identity.item as insItem where (linkId = '3764723550987') then {
        insItem.answer as ans -> patient.identifier = create('Identifier') as insC then {
          ans -> insC.system = 'urn:oid:1.2.250.1.213.1.4.2' "insC-system";
          ans -> insC.value = (ans.valueString) "insC-value";
          ans -> insC.type = cc('http://interopsante.org/fhir/CodeSystem/fr-v2-0203', 'INS-C') "insC-type";
        } "set-insC";
      } "extract-ins";

      // Birth date (linkId 5036133558154)
      identity.item as birthItem where (linkId = '5036133558154') then {
        birthItem.answer as ans -> patient.birthDate = (ans.valueDate) "set-birthdate";
      } "extract-birthdate";

      // Death date (linkId 5633552097315)
      identity.item as deathItem where (linkId = '5633552097315') then {
        deathItem.answer as ans then {
          ans.value as deathDate then {
            deathDate -> patient.deceased = cast(deathDate, 'dateTime') as deceasedDate then {
      // Death source (linkId 9098810065693)
              ans.item as deathSource where (linkId = '9098810065693') then {
                deathSource.answer as sourceAns -> deceasedDate.extension = create('Extension') as ext then {
                  sourceAns -> ext.url = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/death-source' "death-source-url";
                  sourceAns.value as sourceVal -> ext.value = (sourceVal.display) "deathSource";
                } "set-death-source-ext";
              } "extract-death-source";
            } "set-deceased-date";
          } "navigate-death-date";
        } "navigate-death" ;
      } "extract-death";

      // Multiple birth rank (linkId 6931296968515)
      identity.item as multipleItem where (linkId = '6931296968515') then {
        multipleItem.answer as ans then {
          ans.value as birthRank -> patient.multipleBirth = birthRank "set-multiple-birth";
        } "navigate-value";
      } "extract-multiple-birth";

    } "process-identity";
  } "process-sociodemographics";

  // Gender (linkId=3894630481120) from PMSI data (linkId=2825244231605)
  src.item as pmsiGroup where (linkId = '2825244231605') then {
    pmsiGroup.item as sexItem where (linkId = '3894630481120') then {
      sexItem.answer as ans then {
        ans.value as valueCoding -> patient.gender = translate(valueCoding, 'https://interop.aphp.fr/ig/fhir/dm/ConceptMap/dpi-gender-2-hl7-gender', 'code') "set-gender";
      } "extract-gender";
    } "process-gender" ;
  } "process-pmsi-group";

  // Address from Socio-demo (linkId=4647259356106) > Environnement (linkId 5491974639955) and pmsi data (linkId 2825244231605) > Code géographique de résidence (linkId=2446369196222)
  src where (repeat(item).where(linkId='5491974639955').exists() or repeat(item).where(linkId='2446369196222').exists()) -> patient.address = create('Address') as patientAddress then {
    src.item as socioDemo where (linkId = '4647259356106') then {
      socioDemo.item as environnement where (linkId = '5491974639955') then {
      
        // Geographic coordinates (linkId=3816475533472) : latitude (linkId=3709843054556) and longitude (linkId=7651448032665)
        environnement.item as coordinates where (linkId = '3816475533472') -> patientAddress.extension as geolocationExtension, 
          geolocationExtension.url = 'http://hl7.org/fhir/StructureDefinition/geolocation' then {
            // latitudes
            coordinates.item as latitudeItem where (linkId = '3709843054556') -> geolocationExtension.extension as tgtLat then {
              latitudeItem.answer as latitudeAnswer -> tgtLat.url = 'latitude', 
              tgtLat.value = (latitudeAnswer.valueDecimal) "set-latitude";
          } "process-latitude";
            // longitude
            coordinates.item as longitudeItem where (linkId = '7651448032665') -> geolocationExtension.extension as tgtLong then {
              longitudeItem.answer as longitudeAnswer -> tgtLong.url = 'longitude', 
              tgtLong.value = (longitudeAnswer.valueDecimal) "set-longitude";
          } "process-longitude";
            // date de recueil (linkId=1185653257776) start and end date of address validity - je fais l'hypothèse que tous les éléments d'adresses sont recueillis le même jour (le Questionnaire contient trois date de recueil différente, le csv n'en propose qu'une) et je prend la date lié au géocodage. J'ai pas trouvé de solution triviale pour gérer les trois champs (créer une address pour chaque date de recueil différent ? et effectuer les controle sur les dates.)
            coordinates.item as validityDateItem where (linkId = 1185653257776) -> patientAddress.period as addressPeriod then {
              validityDateItem.answer as validityDateAnswer then {
                validityDateAnswer.value as validityDateValue-> addressPeriod.start = cast(validityDateValue, 'dateTime'),
                  addressPeriod.end = cast(validityDateValue, 'dateTime') "set-period";
              } "navigate-collection-date";
            } "process-validity-date";
        } "process-coordinates";

        // IRIS (linkId=7621032273792)
        environnement.item as irisItem where (linkId = '7621032273792') then {
          irisItem.answer as irisAnswer then {
            irisAnswer.value as irisCoding -> patientAddress.line = ('IRIS : ' + irisCoding.code) as irisLine, 
              irisLine.extension = create('Extension') as irisExtension, 
              irisExtension.url = 'http://hl7.org/fhir/StructureDefinition/iso21090-ADXP-censusTract', 
              irisExtension.value = (irisCoding.code.toString()) "set-iris-line" ;
          } "navigate";
        } "process-iris";
      } "process-environment";
    } "process-sociodemographics";

    // PMSI code residence
    src.item as pmsiGroup where (linkId = '2825244231605') then {
      pmsiGroup.item as pmsiResidenceCodeItem where (linkId = '2446369196222') then {
        pmsiResidenceCodeItem.answer as pmsiResidenceCodeAnswer then {
          pmsiResidenceCodeAnswer.value as pmsiResidenceCodeValue -> patientAddress.extension = create('Extension') as pmsiResidenceCodeExtension, 
            pmsiResidenceCodeExtension.url = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/PmsiCodeGeo' ,
            pmsiResidenceCodeExtension.value = (pmsiResidenceCodeValue.code) "SetResidencePmsi";
        } "pmsi-resedence-code-extension";
      } "process-pmsi-residence-code";
    } "process-pmsi-group";
  } "address-creation";
}

// Group: Create Encounters from PMSI data
group CreateEncounters(source src : QuestionnaireResponse, target patient : Patient, target bundle : Bundle) {
  // PMSI data group (linkId 2825244231605) - Note: This can repeat in the structure
  src.item as pmsiItem where (linkId = '2825244231605') -> bundle.entry as encounterEntry then {
    pmsiItem -> encounterEntry.resource = create('Encounter') as encounter then {
      pmsiItem -> encounter.id = uuid() "encounter-id";
      pmsiItem -> encounter.meta = create('Meta') as meta then {
        pmsiItem -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMEncounter' "encounter-profile";
      } "encounter-meta";

      // Identifier (required)
      src -> encounter.identifier as encounterIdentifier,
        encounterIdentifier.system = 'https://interop.aphp.fr/info/Encouter/demo-dm',
        encounterIdentifier.value = uuid(),
        encounterIdentifier.type = cc('https://hl7.fr/ig/fhir/core/CodeSystem/fr-core-cs-identifier-type','VN') "setEncounterIdentifier";
      
      // Status (required)
      src -> encounter.status = 'finished' "setEncounterStatus";

      // Class (required) - les exemples sont clairement du PMSI MCO donc c'est bien, mais on n'a pas cette information...
      src -> encounter.class = c('http://terminology.hl7.org/CodeSystem/v3-ActCode','IMP') "setEncounterClass";

      // Link to patient
      src -> encounter.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

      // Encounter period (start: 5991443718282, end: 6114780320846)
      pmsiItem where (repeat(item).where(linkId='5991443718282').exists() or repeat(item).where(linkId ='6114780320846').exists()) -> encounter.period as period then {
        pmsiItem.item as startItem where (linkId = '5991443718282') then {
          startItem.answer as ans -> period.start = (ans.valueDate) "set-start";
        } "extract-start";
        pmsiItem.item as endItem where (linkId = '6114780320846') then {
          endItem.answer as ans -> period.end = (ans.valueDate) "set-end";
        } "extract-end";
      } "set-period";

      // Mode d'entrée (linkId 6172398101212) et de sortie (linkId 3354867075704)
      pmsiItem -> encounter.hospitalization as hosp then {
        pmsiItem.item as modeInItem where (linkId = '6172398101212') then {
          modeInItem.answer as ans -> hosp.admitSource = create('CodeableConcept') as hospModeIn, hospModeIn.coding = (ans.valueCoding) "set-admit-source";
        } "set-hospitalization-in";
        pmsiItem.item as modeOutItem where (linkId = '3354867075704') then {
          modeOutItem.answer as ans -> hosp.dischargeDisposition = create('CodeableConcept') as hospModeOut, hospModeOut.coding = (ans.valueCoding) "set-discharge-source";
        } "set-hospitalization-out";
      } "process-mode-in-out";

      // entry full url
      src -> encounter.id as encId, encounterEntry.fullUrl = append('urn:uuid:', encId) "set-fullUrl";

      // Create nested Conditions and Procedures
      pmsiItem then CreateConditions(pmsiItem, patient, encounter, bundle) "create-conditions";
      pmsiItem then CreateProcedures(pmsiItem, patient, encounter, bundle) "create-procedures";

    } "create-encounter";
  } "encounter-entry";
}

// Group: Create Conditions from nested diagnostics
group CreateConditions(source pmsiItem, target patient : Patient, target encounter : Encounter, target bundle : Bundle) {
  // Diagnostics group (linkId 9391816419630) - can repeat
  pmsiItem.item as diagGroup where (linkId = '9391816419630') -> bundle.entry as conditionEntry then {
    diagGroup -> conditionEntry.resource = create('Condition') as condition then {
      diagGroup -> condition.id = uuid() "condition-id";
      diagGroup -> condition.meta = create('Meta') as meta then {
        diagGroup -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMCondition' "condition-profile";
      } "condition-meta";

      // Link to patient
      diagGroup -> condition.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";
      
      // Link to encounter
      diagGroup -> encounter.id as encId, condition.encounter = create('Reference') as ref, ref.reference = append('Encounter/', encId) "set-encounter";

      // Diagnostic code (linkId 5505101189372) - CIM-10
      diagGroup.item as diagCodeItem where (linkId = '5505101189372') then {
        diagCodeItem.answer as ans -> condition.code = create('CodeableConcept') as code then {
          ans -> code.coding = (ans.valueCoding) "set-coding";
        } "set-code";
      } "extract-diag-code";

      // Type de diagnostic (linkId 6427586743735) - DP/DAS/DR
      diagGroup.item as diagTypeItem where (linkId = '6427586743735') then {
        diagTypeItem.answer as ans -> condition.category = create('CodeableConcept') as category then {
          ans -> category.coding = create('Coding') as coding then {
            ans -> coding.system = 'https://interop.aphp.fr/ig/fhir/dm/CodeSystem/pmsi-mco-diag-type' "diag-type-system";
            ans -> coding.code = (ans.valueCoding.code) "diag-type-code";
            ans -> coding.display = (ans.valueCoding.display) "diag-type-display";
          } "set-category-coding";
        } "set-category";
      } "extract-diag-type";

      // Recorded date (linkId 7114466839467)
      diagGroup.item as dateItem where (linkId = '7114466839467') then {
        dateItem.answer as ans -> condition.recordedDate = (ans.valueDate) "set-recorded-date";
      } "extract-recorded-date";

      // entry full url
      pmsiItem -> condition.id as condId, conditionEntry.fullUrl = append('urn:uuid:', condId) "set-fullUrl";

    } "create-condition";
  } "condition-entry";
}

// Group: Create Procedures from nested actes
group CreateProcedures(source pmsiItem, target patient : Patient, target encounter : Encounter, target bundle : Bundle) {
  // Actes group (linkId 591926901726) - can repeat
  pmsiItem.item as acteGroup where (linkId = '591926901726') -> bundle.entry as procedureEntry then {
    acteGroup -> procedureEntry.resource = create('Procedure') as procedure then {
      acteGroup -> procedure.id = uuid() "procedure-id";
      acteGroup -> procedure.meta = create('Meta') as meta then {
        acteGroup -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMProcedure' "procedure-profile";
      } "procedure-meta";

      // Status
      acteGroup -> procedure.status = 'completed' "setProcedureStatus";

      // Link to patient
      acteGroup -> procedure.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

      // Link to encounter
      acteGroup -> encounter.id as encId, procedure.encounter = create('Reference') as ref, ref.reference = append('Encounter/', encId) "set-encounter";

      // Acte code (linkId 7758110033600) - CCAM
      acteGroup.item as acteCodeItem where (linkId = '7758110033600') then {
        acteCodeItem.answer as ans -> procedure.code = create('CodeableConcept') as code then {
          ans -> code.coding = (ans.valueCoding) "set-coding";
        } "set-code";
      } "extract-acte-code";

      // Date de l'acte (linkId 5066866286682)
      acteGroup.item as dateItem where (linkId = '5066866286682') then {
        dateItem.answer as ans -> procedure.performed = (ans.valueDateTime) "set-performed";
      } "extract-performed-date";

      // Collection date (linkId 9436509453137)
      /*acteGroup.item as collectionDateItem where (linkId = '9436509453137') then {
        collectionDateItem.answer as ans -> procedure.extension = create('Extension') as ext then {
          ans -> ext.url = 'http://hl7.org/fhir/StructureDefinition/data-collection-date' "date-url";
          ans -> ext.value = (ans.valueDate) "date-value";
        } "set-collection-date";
      } "extract-collection-date";*/

      // entry full url
      pmsiItem -> procedure.id as procId, procedureEntry.fullUrl = append('urn:uuid:', procId) "set-fullUrl";

    } "create-procedure";
  } "procedure-entry";
}

// Group: Create Medication Requests
group CreateMedicationRequests(source src : QuestionnaireResponse, target patient : Patient, target bundle : Bundle) {
  // Exposition médicamenteuse (linkId 817801935685) - repeating
  src.item as medExpoGroup where (linkId = '817801935685') -> bundle.entry as medReqEntry then {
    medExpoGroup -> medReqEntry.resource = create('MedicationRequest') as medReq then {
      medExpoGroup -> medReq.id = uuid() "medreq-id";
      medExpoGroup -> medReq.id as medReqId, medReqEntry.fullUrl = append('urn:uuid:', medReqId) "set-fullUrl";

      medExpoGroup -> medReq.meta = create('Meta') as meta then {
        medExpoGroup -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMMedicationRequest' "medreq-profile";
      } "medreq-meta";

      // Link to patient
      medExpoGroup -> medReq.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";
    
      // Status and intent - default value for these two properties are "completed" and "original-order"
      medExpoGroup -> medReq.status = 'completed' "set-status";
      medExpoGroup -> medReq.intent = 'original-order' "set-intent";

      // Médicament prescrit (linkId 156631794800)
      medExpoGroup.item as prescribedMedicationItem where (linkId = '156631794800') then {
        prescribedMedicationItem.answer as prescribedMedicationAnswer then {
          prescribedMedicationAnswer.value as prescribedMedicationValue -> medReq.medication = create('Reference') as medReqMedication, medReqMedication.display = (prescribedMedicationValue) "setMedicationReference";
        // nested items
          // note.display (ATCs)
          prescribedMedicationAnswer.item as atcItem where (linkId = '1923143398283') then {
            atcItem.answer as atcAnswer -> medReq.note as medReqNote then {
              atcAnswer.value as atcValueCoding -> medReqNote.text = ('ATC - ' + atcValueCoding.code + ' : ' + atcValueCoding.display) "setAnnotation" ;
            } "navigate";
          } "extractAtc";
          // route
          prescribedMedicationAnswer.item as routeItem where (linkId = '387026794874') then {
            routeItem.answer as routeAnswer -> medReq.dosageInstruction as medReqDos, medReqDos.route = create('CodeableConcept') as medReqRoute, medReqRoute.coding = (routeAnswer.valueCoding) then {
          // medication Duration
              medExpoGroup.item as posologieGroup where (linkId = '6348237104421') then {
                posologieGroup.item as startDateItem where (linkId = '316347573327') then {
                  startDateItem.answer as startDateAnswer then {
                    posologieGroup.item as endDateItem where (linkId = '429570775935') then {
                      endDateItem.answer as endDateAnswer -> medReqDos.timing as medReqTiming, medReqTiming.repeat as medReqTimingRep, medReqTimingRep.bounds = create('Period') as medReqPeriod, medReqPeriod.start = (startDateAnswer.valueDate), medReqPeriod.end = (endDateAnswer.valueDate) "setDates";
                    } "extractEndDate";
                  } "navigateEndDate";
                } "extractStartDate";
              } "extractPosologie";
            }"setRoute";
          } "extractRoute";
        } "extractMedicationInformation";
      } "setMedReq";

    // Create Medication Administrations
    medExpoGroup then CreateMedicationAdministrations(medExpoGroup, patient, bundle, medReq) "create-medication-administrations";

    } "create-medreq";

  } "medreq-entry";
}

// Group: Create Medication Administrations
group CreateMedicationAdministrations(source src, target patient : Patient, target bundle : Bundle, target medReq : MedicationRequest) {
  // Exposition médicamenteuse (linkId 817801935685)
  src.item as adminGroup where (linkId = '5720103839343') -> bundle.entry as medAdminEntry then {
    adminGroup -> medAdminEntry.resource = create('MedicationAdministration') as medAdmin then {
      adminGroup -> medAdmin.id = uuid() "medadmin-id";
      adminGroup -> medAdmin.id as medAdmId, medAdminEntry.fullUrl = append('urn:uuid:', medAdmId) "set-fullUrl";
      adminGroup -> medAdmin.meta = create('Meta') as meta then {
        adminGroup -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMMedicationAdministration' "medadmin-profile";
      } "medadmin-meta";

      // Link to patient
      adminGroup -> medAdmin.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

      // Link to medication request
      src -> medAdmin.request = create('Reference') as ref, medReq.id as medReqId, ref.reference = append('MedicationRequest/',medReqId) "setMedReq";

      // Status
      adminGroup -> medAdmin.status = 'completed' "set-status";

      // Administration period
      adminGroup where (repeat(item).where(linkId='1443558617577').exists() and repeat(item).where(linkId='780829110731').exists()) then {
        adminGroup.item as startDateItem where (linkId = '1443558617577') then {
          startDateItem.answer as startDateAnswer then {
            adminGroup.item as endDateItem where (linkId = '780829110731') then {
              endDateItem.answer as endDateAnswer -> medAdmin.effective = create('Period') as adminPeriod, adminPeriod.start = (startDateAnswer.valueDateTime), adminPeriod.end = (endDateAnswer.valueDateTime) "setDates";
            } "extractEndDate";
          } "navigateStartDate";
        } "extractStartDate";
      } "setAdministrationPeriod";

      // Administration date
      adminGroup where (repeat(item).where(linkId='1443558617577').exists() xor repeat(item).where(linkId='780829110731').exists()) then {
        adminGroup.item as startDateItem where (linkId = '1443558617577') then {
          startDateItem.answer as startDateAnswer -> medAdmin.effective = (startDateAnswer.valueDateTime) "setAdminDate";
        } "setStartDateAsAdminDate";
        adminGroup.item as endDateItem where (linkId = '780829110731') then {
          endDateItem.answer as endDateAnswer -> medAdmin.effective = (endDateAnswer.valueDateTime) "setAdminDate";
        } "setEndDateAsAdminDate";
      } "setAdministrationDate";

      // Dosage
      adminGroup.item as adminQuantityItem where (linkId = '4765772671997') then {
        adminQuantityItem.answer as adminQuantityAnswer -> medAdmin.dosage as medAdminDosage, medAdminDosage.dose = (adminQuantityAnswer.valueQuantity) as medAdminDose, medAdminDose.system ='http://unitsofmeasure.org' then {

          // Administered Drug
          src.item as medicationItem where (linkId = '266852453304') then {
            medicationItem.answer as medicationAnswer -> medAdmin.medication = create('Reference') as medicationRef, medicationRef.display = (medicationAnswer.valueString) then {
              
              // Route
              medicationAnswer.item as routeItem where (linkId = '811931484859') then {
                routeItem.answer as routeAnswer -> medAdminDosage.route = create('CodeableConcept') as routeCC, routeCC.coding = (routeAnswer.valueCoding) "setRoute";
              } "navigateRoute";

              // ATC as note
              medicationAnswer.item as atcItem where (linkId = '631972144976') then {
                atcItem.answer as atcAnswer then {
                  atcAnswer.value as atcValue -> medAdmin.note as medAminNote, medAminNote.text = ('ATC : ' + atcValue.code + ' - ' + atcValue.display) "setAnnotation";
                } "navigate"; 
              } "extractAtc";

            }"setMedication";
          }"extractMedication";

        }"setDosage";

      } "extractDosage";

    } "createMedAdmin";
  } "createMedAdminEntry";
}

// Group: Create Vital Sign Observations
group CreateVitalSignObservations(source src : QuestionnaireResponse, target patient : Patient, target bundle : Bundle) {
  // Examen clinique (linkId 214880328197)
  src.item as examGroup where (linkId = '214880328197') then {

    // Dossier de soins (linkId 305831246173) - repeating
    examGroup.item as dossierGroup where (linkId = '305831246173') then {

      // Height (linkId 4846902346416)
      dossierGroup.item as heightItem where (linkId = '4846902346416') then {
        heightItem.answer as ans -> bundle.entry as obsEntry then {
          ans -> obsEntry.resource = create('Observation') as obs then {
            ans -> obs.id = uuid() "obs-id";
            ans -> obs.id as obsId, obsEntry.fullUrl = append('urn:uuid:', obsId) "set-fullUrl";
            ans -> obs.meta = create('Meta') as meta then {
              ans -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationBodyHeight' "obs-profile";
            } "obs-meta";

            ans -> obs.status = 'final' "obs-status";
            ans -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category', 'vital-signs') "obs-category";
            ans -> obs.code = cc('http://loinc.org', '8302-2', 'Body height') "obs-code";

            // Link to patient
            ans -> obs.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";
            
            // Value
            ans.value as ansValue then { 
              ansValue -> obs.value = create('Quantity') as obsValue,
              obsValue.value = (ansValue.value),
              obsValue.unit = (ansValue.unit),
              obsValue.code = (ansValue.unit),
              obsValue.system = 'http://unitsofmeasure.org' "setQuantity";
            } "setValue";

            // Effective date from nested item
            ans.item as dateItem where (linkId.contains('941821315470')) then {
              dateItem.answer as dateAns then {
                dateAns.value as dateValue -> obs.effective = cast(dateValue, 'dateTime') "set-effective";
              } "createEffectiveDateTime";
            } "extract-effective";
          } "create-height-obs";
        } "height-obs-entry";
      } "extract-height";

      // Weight (linkId 451513217936)
      dossierGroup.item as weightItem where (linkId = '451513217936') then {
        weightItem.answer as ans -> bundle.entry as obsEntry then {
          ans -> obsEntry.resource = create('Observation') as obs then {
            ans -> obs.id = uuid() "obs-id";
            ans -> obs.id as obsId, obsEntry.fullUrl = append('urn:uuid:', obsId) "set-fullUrl";
            ans -> obs.meta = create('Meta') as meta then {
              ans -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationBodyWeight' "obs-profile";
            } "obs-meta";

            ans -> obs.status = 'final' "obs-status";
            ans -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category', 'vital-signs') "obs-category";
            ans -> obs.code = cc('http://loinc.org', '29463-7', 'Body weight') "obs-code";

            // Link to patient
            ans -> obs.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

            // Value
            ans.value as ansValue then { 
              ansValue -> obs.value = create('Quantity') as obsValue,
              obsValue.value = (ansValue.value),
              obsValue.unit = (ansValue.unit),
              obsValue.code = (ansValue.unit),
              obsValue.system = 'http://unitsofmeasure.org' "setQuantity";
            } "setValue";

            // Effective date from nested item
            ans.item as dateItem where (linkId.contains('151269044052')) then {
              dateItem.answer as dateAns -> obs.effective = (dateAns.valueDate) "set-effective";
            } "extract-effective";
          } "create-weight-obs";
        } "weight-obs-entry";
      } "extract-weight";

      // Blood Pressure - systolic and diastolic BP in the same repetition of dossierGroup are considered to be measured at the same time 
      dossierGroup as bpGroup where (repeat(item).where(linkId='4160905247955').exists() or repeat(item).where(linkId='848797127998').exists()) -> bundle.entry as obsEntry, 
        obsEntry.resource = create('Observation') as obs then {
        bpGroup -> obs.id = uuid() "obs-id";
        bpGroup -> obs.id as obsId, obsEntry.fullUrl = append('urn:uuid:', obsId) "set-fullUrl";
        bpGroup -> obs.meta = create('Meta') as meta then {
          bpGroup -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationBloodPressure' "obs-profile";
        } "obs-meta";

        // Date (the systolic BP date is considered first)
        bpGroup.item as sysItem where (linkId = '4160905247955') then {
          sysItem.answer as sysAns then {
            sysAns.item as sysDateItem where (linkId = '987654638442') then {
              sysDateItem.answer as sysDateAnswer -> obs.effective = (sysDateAnswer.valueDate) "setSysDate";
            } "navSysDateItem";
          } "navSysAnswer";
        } "navSysItem"; 

        // Date, if there is only a diastolyc pressure
        bpGroup as diaOnlyGroup where (repeat(item).where(linkId='848797127998').exists() and repeat(item).where(linkId='4160905247955').exists().not()) then {
          bpGroup.item as diaItem where (linkId = '848797127998') then {
            diaItem.answer as diaAns then {
              diaAns.item as diaDateItem where (linkId = '820981783585') then {
                diaDateItem.answer as diaDateAnswer -> obs.effective = (diaDateAnswer.valueDate) "setDiaDate";
              } "navDiaDateItem";
            } "navDiaAnswer";
          } "navDiaItem";
        } "checkDiaGroupOnlyExistence";
        
        //Status
        bpGroup -> obs.status = 'final' "obs-status";

        // Category
        bpGroup -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category', 'vital-signs') "obs-category";

        // Code
        bpGroup -> obs.code = cc('http://loinc.org', '85354-9', 'Blood pressure panel') "obs-code";

        // Link to patient
        bpGroup -> obs.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

        // Systolic component
        bpGroup.item as sysItem where (linkId = '4160905247955') then {
          sysItem.answer as sysAns then {
              sysAns.value as sysAnsValue then {
                sysAnsValue -> obs.component as obsSysComp,
                  obsSysComp.code = cc('http://loinc.org', '8480-6', 'Systolic blood pressure'),
                  obsSysComp.value = create('Quantity') as obsSysCompValue,
                  obsSysCompValue.value = (sysAnsValue.value),
                  obsSysCompValue.unit = (sysAnsValue.unit),
                  obsSysCompValue.code = (sysAnsValue.unit),
                  obsSysCompValue.system = 'http://unitsofmeasure.org' "setQuantity";
              } "navSysValue";
            } "navSysAnswer";
          } "processSystolicValue";

        // Diastolic component
        bpGroup.item as diaItem where (linkId = '848797127998') then {
          diaItem.answer as diaAns then {
            diaAns.value as diaAnsValue then {
              diaAnsValue -> obs.component as obsDiaComp,
                obsDiaComp.code = cc('http://loinc.org', '8462-4', 'Diastolic blood pressure'),
                obsDiaComp.value = create('Quantity') as obsDiaCompValue,
                obsDiaCompValue.value = (diaAnsValue.value),
                obsDiaCompValue.unit = (diaAnsValue.unit),
                obsDiaCompValue.code = (diaAnsValue.unit),
                obsDiaCompValue.system = 'http://unitsofmeasure.org' "setCodeAndQuantity";
            } "navDiaValue";
          } "navDiaAnswer";
        } "processDiastolicValue";

      }"createBpObservation";
    } "process-dossier";
  } "process-exam";
}

// Group: Create Laboratory Observations
group CreateLaboratoryObservations(source src : QuestionnaireResponse, target patient : Patient, target bundle : Bundle) {
  // Biologie group (linkId 7702944131447)
  src.item as bioGroup where (linkId = '7702944131447') then {

    // Fonction rénale (linkId 5241323453538)
    bioGroup.item as renalGroup where (linkId = '5241323453538') then {
      // Urée (linkId 7169026818760)
      renalGroup.item as ureaItem where (linkId = '7169026818760') then CreateLabObservation(ureaItem, patient, bundle) "extract-urea";

      // Créatininémie (linkId 500408205043)
      renalGroup.item as creatItem where (linkId = '500408205043') then CreateLabObservation(creatItem, patient, bundle) "extract-creat";

      // DFG (linkId 786621340679)
      renalGroup.item as dfgItem where (linkId = '786621340679') then CreateLabObservation(dfgItem, patient, bundle) "extract-dfg";
    } "process-renal";

    // Hémogramme (linkId 419282985970)
    bioGroup.item as hemoGroup where (linkId = '419282985970') then {
      // Leucocytes (linkId 210077225604)
      hemoGroup.item as leukoItem where (linkId = '210077225604') then CreateLabObservation(leukoItem, patient, bundle) "extract-leuko";

      // Hémoglobine (linkId 304159088493)
      hemoGroup.item as hemoItem where (linkId = '304159088493') then CreateLabObservation(hemoItem, patient, bundle) "extract-hemo";

      // Hématocrite (linkId 813863316705)
      hemoGroup.item as hematItem where (linkId = '813863316705') then CreateLabObservation(hematItem, patient, bundle) "extract-hemat";

      // Erythrocytes (linkId 459731866614)
      hemoGroup.item as eryItem where (linkId = '459731866614') then CreateLabObservation(eryItem, patient, bundle) "extract-ery";

      // VGM (linkId 163624088831)
      hemoGroup.item as vgmItem where (linkId = '163624088831') then CreateLabObservation(vgmItem, patient, bundle) "extract-vgm";

      // Plaquettes (linkId 794156787471)
      hemoGroup.item as platItem where (linkId = '794156787471') then CreateLabObservation(platItem, patient, bundle) "extract-plat";

      // Neutrophiles (linkId 961905168477)
      hemoGroup.item as neutItem where (linkId = '961905168477') then CreateLabObservation(neutItem, patient, bundle) "extract-neut";

      // Lymphocytes (linkId 695150914696)
      hemoGroup.item as lymphItem where (linkId = '695150914696') then CreateLabObservation(lymphItem, patient, bundle) "extract-lymph";

      // Eosinophiles (linkId 700490326748)
      hemoGroup.item as eosiItem where (linkId = '700490326748') then CreateLabObservation(eosiItem, patient, bundle) "extract-eosi";

      // Monocytes (linkId 168661900522)
      hemoGroup.item as monoItem where (linkId = '168661900522') then CreateLabObservation(monoItem, patient, bundle) "extract-mono";

      // TP (linkId 658898841893)
      hemoGroup.item as tpItem where (linkId = '658898841893') then CreateLabObservation(tpItem, patient, bundle) "extract-tp";

      // TCA (linkId 795145096241)
      hemoGroup.item as tcaItem where (linkId = '795145096241') then CreateLabObservation(tcaItem, patient, bundle) "extract-tca";
    } "process-hemo";

    // Bilan hépatique (linkId 796308115381)
    bioGroup.item as liverGroup where (linkId = '796308115381') then {
      // ASAT (linkId 715226319725)
      liverGroup.item as asatItem where (linkId = '715226319725') then CreateLabObservation(asatItem, patient, bundle) "extract-asat";

      // ALAT (linkId 876439410327)
      liverGroup.item as alatItem where (linkId = '876439410327') then CreateLabObservation(alatItem, patient, bundle) "extract-alat";

      // GGT (linkId 287545455976)
      liverGroup.item as ggtItem where (linkId = '287545455976') then CreateLabObservation(ggtItem, patient, bundle) "extract-ggt";

      // PAL (linkId 508269571594)
      liverGroup.item as palItem where (linkId = '508269571594') then CreateLabObservation(palItem, patient, bundle) "extract-pal";

      // Bilirubine totale (linkId 927344090061)
      liverGroup.item as bilTotItem where (linkId = '927344090061') then CreateLabObservation(bilTotItem, patient, bundle) "extract-biltot";

      // Bilirubine conjuguée (linkId 208196328453)
      liverGroup.item as bilConjItem where (linkId = '208196328453') then CreateLabObservation(bilConjItem, patient, bundle) "extract-bilconj";
    } "process-liver";

    // autres (Métabolisme glucidique) (linkId 334039497382)
    bioGroup.item as glucoseGroup where (linkId = '334039497382') then {
      // Glycémie à jeun (linkId 273778921448)
      glucoseGroup.item as glycItem where (linkId = '273778921448') then CreateLabObservation(glycItem, patient, bundle) "extract-glyc";

      // HbA1c (linkId 632894677152)
      glucoseGroup.item as hba1cItem where (linkId = '632894677152') then CreateLabObservation(hba1cItem, patient, bundle) "extract-hba1c";
    } "process-glucose";

  } "process-biology";
}

// Helper: Create a single Laboratory Observation
group CreateLabObservation(source item, target patient : Patient, target bundle : Bundle) {
  item.answer as ans -> bundle.entry as obsEntry then {
    ans -> obsEntry.resource = create('Observation') as obs then {
      ans -> obs.id = uuid() "obs-id";
      ans -> obs.id as obsId, obsEntry.fullUrl = append('urn:uuid:', obsId) "set-fullUrl";
      ans -> obs.meta = create('Meta') as meta then {
        item where (linkId = '210077225604') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryLeucocytes' "obs-profile";
        item where (linkId = '304159088493') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryHemoglobine' "obs-profile";
        item where (linkId = '813863316705') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryHematocrite' "obs-profile";
        item where (linkId = '459731866614') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryErythrocytes' "obs-profile";
        item where (linkId = '163624088831') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryVgm' "obs-profile";
        item where (linkId = '794156787471') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryPlaquettes' "obs-profile";
        item where (linkId = '961905168477') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryNeutrophiles' "obs-profile";
        item where (linkId = '695150914696') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryLymphocytes' "obs-profile";
        item where (linkId = '700490326748') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryEosinophiles' "obs-profile";
        item where (linkId = '168661900522') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryMonocytes' "obs-profile";
        item where (linkId = '658898841893') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryTp' "obs-profile";
        item where (linkId = '795145096241') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryTca' "obs-profile";
        item where (linkId = '715226319725') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryAsat' "obs-profile";
        item where (linkId = '876439410327') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryAlat' "obs-profile";
        item where (linkId = '287545455976') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryGgt' "obs-profile";
        item where (linkId = '508269571594') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryPal' "obs-profile";
        item where (linkId = '927344090061') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryBilirubineTotale' "obs-profile";
        item where (linkId = '208196328453') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryBilirubineConjuguee' "obs-profile";
        item where (linkId = '273778921448') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryGlycemieAJeun' "obs-profile";
        item where (linkId = '632894677152') -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationLaboratoryHbA1c' "obs-profile";
      } "obs-meta";

      // a default status (final) is set up but replaced depending on the QR content. 
      ans -> obs.status = 'final' "setStatus";

      // Category
      ans -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','laboratory') "set-category";

      // Link to patient
      ans -> obs.subject = create('Reference') as ref, patient.id as patId, ref.reference = append('Patient/', patId) "set-subject";

      // generic items : effective date, status - corresponds to properties that are not in the component property, if any.
      ans.item as effectiveItem where (linkId in ('554606544143'|'578695171519'|'471894150678'|'717817548701'|'394008758803'|'766800452654'|'504411027287'|'255457777106'|'548571990081'|'732609383475'|'773480648641'|'379165914699'|'884511824515'|'881040341675'|'191793018592'|'576101241859'|'943765996333'|'481261763129'|'632111374848'|'249760336391'|'339688288601'|'279176538278'|'600008092136')) then {
        effectiveItem.answer as effectiveAnswer -> obs.effective = (effectiveAnswer.valueDateTime) "setEffective";
      } "extract-effective";
      ans.item as statusItem where (linkId in ('5827729311488'|'844270911527'|'958333284757'|'379339292477'|'536131874715'|'490513081670'|'639812945465'|'473989322417'|'357661538905'|'427000822533'|'335226309641'|'429387291763'|'899626356193'|'914082125507'|'554823851678'|'650432942630'|'510595115954'|'711131346437'|'844988086697'|'255050517895'|'271848662924'|'754581131140'|'288013601711')) then {
        statusItem.answer as statusAnswer then {
          statusAnswer.value as statusValue -> obs.status = translate(statusValue,'https://interop.aphp.fr/ig/fhir/dm/ConceptMap/dpi-lab-status-2-hl7-obs-status', 'code') "setGenericStatus";
        } "translateStatus";
      } "extractStatus";

      // specific items for generic analysis - LOINC code, reference ranges - specific items correspond to propoerties that can both be nested in the component property or at the root - generic analysis are those that consist in one code and one value i.e. they dont take advantage of the component property.
      ans.item as loincItem where (linkId in ('8712072639576'|'971737782589'|'695484403752'|'597082091886'|'814457693114'|'274747215145'|'117718572179'|'555876654291'|'663015267867'|'283372872777'|'651849853076'|'219513623269'|'757363598159'|'698926487710'|'688754894578'|'726897332003'|'657001208566'|'572609665342'|'398039571990'|'305948197507')) then {
        loincItem.answer as loincAnswer -> obs.code = create('CodeableConcept') as obsCC, 
          obsCC.coding = (loincAnswer.valueCoding) "setLoincCode";
        ans -> obs.value = (ans.valueQuantity) "set-value";
      } "extract-loinc";
      ans.item as lowerRangeItem where (linkId in ('3753371809615'|'147832677057'|'404489113722'|'728299430368'|'823112437353'|'472419988966'|'491138393211'|'501223312827'|'217415906562'|'162927062211'|'693935514182'|'253311419093'|'584123691679'|'581883215378'|'529114499294'|'334934131934'|'439478297194'|'454797820953'|'228927847562'|'804046966782')) then {
        lowerRangeItem.answer as lowerRangeAnswer -> obs.referenceRange as obsRange, 
          obsRange.low = (lowerRangeAnswer.valueQuantity) "setLowerRange";
      } "extractLowerRange";
      ans.item as upperRangeItem where (linkId in ('7284688712273'|'175377691907'|'849522090945'|'296878978572'|'567971363132'|'781499407274'|'859081902103'|'454392779990'|'719012554421'|'801523695037'|'241118076478'|'706043904552'|'935082505777'|'594636277238'|'930639734905'|'989367019315'|'363409811810'|'741523817402'|'269189312131'|'703927692674')) then {
        upperRangeItem.answer as upperRangeAnswer -> obs.referenceRange as obsRange, 
          obsRange.high = (upperRangeAnswer.valueQuantity) "setUpperRange";
      } "extractUpperRange";

      // specific items for specific analysis - LOINC code, effective date, status, reference ranges - specific items correspond to propoerties that can both be nested in the component property or at the root - specific analysis are those that imply the use of the component property. 
        // creatininemia
      ans.item as loincItem where (linkId in ('977768150991')) then {
        ans -> obs.code = cc('http://loinc.org','45066-8') "setLoincCode";
        loincItem.answer as loincAnswer -> obs.component as component,
          component.code = create('CodeableConcept') as compoCC, 
          compoCC.coding = (loincAnswer.valueCoding),
          component.value = (ans.valueQuantity) "setLoincComponentCodeAndValue";
      } "extract-loinc";
      ans.item as lowerRangeItem where (linkId in ('488869963230')) then {
        lowerRangeItem.answer as lowerRangeAnswer -> obs.component as component,
          component.referenceRange as compoRange, 
          compoRange.low = (lowerRangeAnswer.valueQuantity) "setLowerRange";
      } "extractLowerRange";
      ans.item as upperRangeItem where (linkId in ('984480974142')) then {
        upperRangeItem.answer as upperRangeAnswer -> obs.component as component,
        component.referenceRange as compoRange, 
          compoRange.high = (upperRangeAnswer.valueQuantity) "setUpperRange";
      } "extractUpperRange";

        // glomerular filtration rate
      ans.item as loincItem where (linkId in ('974417569313')) then {
        ans -> obs.code = cc('http://loinc.org','45066-8') "setLoincCode";
        loincItem.answer as loincAnswer -> obs.component as component,
          component.code = (loincAnswer.valueCoding),
          component.value = (ans.valueQuantity) "setLoincComponentCodeAndValue";
      } "extract-loinc";
      ans.item as lowerRangeItem where (linkId in ('809916645548')) then {
        lowerRangeItem.answer as lowerRangeAnswer -> obs.component as component,
          component.referenceRange as compoRange, 
          compoRange.low = (lowerRangeAnswer.valueQuantity) "setLowerRange";
      } "extractLowerRange";
      ans.item as upperRangeItem where (linkId in ('323493215684')) then {
        upperRangeItem.answer as upperRangeAnswer -> obs.component as component,
        component.referenceRange as compoRange, 
          compoRange.high = (upperRangeAnswer.valueQuantity) "setUpperRange";
      } "extractUpperRange";

              // Partial thromboplastin time (TCA)
      ans.item as loincItem where (linkId in ('664804454505')) then {
        ans -> obs.code = cc('http://loinc.org','50197-3') "setLoincCode";
        loincItem.answer as loincAnswer -> obs.component as component,
          component.code = (loincAnswer.valueCoding),
          component.value = (ans.valueQuantity) "setLoincComponentCodeAndValue";
      } "extract-loinc";
      ans.item as lowerRangeItem where (linkId in ('680824187549')) then {
        lowerRangeItem.answer as lowerRangeAnswer -> obs.component as component,
          component.referenceRange as compoRange, 
          compoRange.low = (lowerRangeAnswer.valueQuantity) "setLowerRange";
      } "extractLowerRange";
      ans.item as upperRangeItem where (linkId in ('293820463780')) then {
        upperRangeItem.answer as upperRangeAnswer -> obs.component as component,
        component.referenceRange as compoRange, 
          compoRange.high = (upperRangeAnswer.valueQuantity) "setUpperRange";
      } "extractUpperRange";

    } "create-obs";

  } "obs-entry";
}

group CreateHabitusObservation(source src : QuestionnaireResponse, target patient : Patient, target bundle : Bundle) {
  src.item as clinGroup where (linkId = '214880328197') then {
    clinGroup. item as lifeStyleGroup where (linkId = '1693164086678') then {
      lifeStyleGroup.item as habitusItem-> bundle.entry as obsEntry,
        obsEntry.resource = create('Observation') as obs,
        obs.id = uuid() as obsId,
        obsEntry.fullUrl = append('urn:uuid:', obsId),
        // a default status (final) is set up but replaced depending on the QR content. 
        obs.status = 'final',
        // Link to patient
        obs.subject = create('Reference') as ref, 
        patient.id as patId, 
        ref.reference = append('Patient/', patId) then {

          // Tobaco
          habitusItem as tobacoItem where (linkId = '6516656388671') then {
            // Meta
            habitusItem -> obs.meta = create('Meta') as meta then {
              habitusItem -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationSmokingStatus' "obs-profile";
            } "obs-meta";
            // Category
            habitusItem -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','social-history') "set-category";
            // Code
            habitusItem -> obs.code = cc('http://loinc.org','72166-2') "setCode";
          } "manageTobaco";

          // Alcool
          habitusItem as alcoolItem where (linkId = '8218964737490') then {
            // Meta
            habitusItem -> obs.meta = create('Meta') as meta then {
              habitusItem -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationAlcoholUseStatus' "obs-profile";
            } "obs-meta";
            // Category
            habitusItem -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','social-history') "set-category";
            // Code
            habitusItem -> obs.code = cc('http://loinc.org','11331-6') "setCode";
          } "manageAlcool";

          // Drug
          habitusItem as drugItem where (linkId = '7063448155344') then {
            // Meta
            habitusItem -> obs.meta = create('Meta') as meta then {
              habitusItem -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationSubstanceUseStatus' "obs-profile";
            } "obs-meta";
            // Category
            habitusItem -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','social-history') "set-category";
            // Code
            habitusItem -> obs.code = cc('http://loinc.org','11343-1') "setCode";
          } "manageDrug";

          // Physical activity
          habitusItem as activityItem where (linkId = '485428113240') then {
            // Meta
            habitusItem -> obs.meta = create('Meta') as meta then {
              habitusItem -> meta.profile = 'https://interop.aphp.fr/ig/fhir/dm/StructureDefinition/DMObservationExerciceStatus' "obs-profile";
            } "obs-meta";
            // Category
            habitusItem -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','social-history') "set-category";
            habitusItem -> obs.category = cc('http://terminology.hl7.org/CodeSystem/observation-category','activity') "set-category";
            // Code
            habitusItem -> obs.code = cc('http://loinc.org','77242-6') "setCode";
          } "manageActivity";

          // Answer
          habitusItem.answer as srcAnswer then {
            srcAnswer.value as srcValue -> obs.value = create('CodeableConcept') as value, 
              value.text = srcValue "setValue";
          } "processValue" ;

        } "fillSpecificHabitusProperty" ;
    } "setObs";
  } "processClinical";
}
